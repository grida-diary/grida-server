plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.13'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.grida'
	version = '0.0.1-SNAPSHOT'

	java {
		sourceCompatibility = 17
		targetCompatibility = 17
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.junit.jupiter:junit-jupiter'

		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

configure(subprojects.findAll {it.name != 'grida-core'}) {
	repositories {
		// yaml importer
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		// spring boot
		implementation 'org.springframework.boot:spring-boot-starter'

		// properties
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

		// yaml importer
		compileOnly 'com.github.kingbbode:spring-boot-custom-yaml-importer:0.3.0'

		// core module
		implementation project(':grida-core')
	}
}
